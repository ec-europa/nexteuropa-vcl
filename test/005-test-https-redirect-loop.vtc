varnishtest "Redirect cache issue"

# Keep replying 200 with stupid body
server s1 -repeat 30 {
       rxreq 
       txresp -body "myjsbody2\n"
} -start


# Basic setup :
varnish v1 -vcl+backend {
  import directors;

  # Basic generated by salt
  include "test/assets/salted.vcl";
  # Default vcl
  include "test/assets/default.vcl";
  # Actual nexteuropa vcl
  include "testing.vcl";

  # Use the random director
  sub vcl_init {
      new cluster = directors.random();
      cluster.add_backend(s1, 1);
  }

  sub vcl_backend_fetch {
    set bereq.backend = cluster.backend();
  }

} -start

# Test cases :
client c1 -repeat 3 {
    # Unauth
    txreq -url "/subsite" -hdr "Host: europa.eu" -hdr "X-Forwarded-Proto: http"
    rxresp
    expect resp.status == 301
    expect resp.http.Cache-Control ~ "s-maxage=0"
    txreq -url "/subsite" -hdr "Host: europa.eu" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200

    # Auth http
    txreq -url "/subsite" -hdr "Host: europa.eu" -hdr "X-Forwarded-Proto: http" 
    rxresp
    expect resp.status == 301
    expect resp.http.Cache-Control ~ "s-maxage=0"
    txreq -url "/subsite" -hdr "Host: europa.eu" -hdr "X-Forwarded-Proto: https" -hdr "Cookie: SSESSc0a59ad24620e681566ae51679e1e824=yes;"
    rxresp
    expect resp.status == 200

    # Bogus auth http/https
    txreq -url "/subsite" -hdr "Host: europa.eu" -hdr "X-Forwarded-Proto: http"  -hdr "Cookie: SESSc0a59ad24620e681566ae51679e1e824=yes;"
    rxresp
    expect resp.status == 301
    expect resp.http.Cache-Control ~ "s-maxage=0"
    txreq -url "/subsite" -hdr "Host: europa.eu" -hdr "X-Forwarded-Proto: http" -hdr "Cookie: SSESSc0a59ad24620e681566ae51679e1e824=yes;"
    rxresp
    expect resp.status == 301
    expect resp.http.Cache-Control ~ "s-maxage=0"
    txreq -url "/subsite" -hdr "Host: europa.eu" -hdr "X-Forwarded-Proto: https" -hdr "Cookie: SSESSc0a59ad24620e681566ae51679e1e824=yes;"
    rxresp
    expect resp.status == 200

    # X-Forward-Proto not set :
    txreq -url "/subsite" -hdr "Host: europa.eu" -hdr "Cookie: SSESSc0a59ad24620e681566ae51679e1e824=yes;"
    rxresp
    expect resp.status == 200
    txreq -url "/subsite" -hdr "Host: europa.eu" -hdr "Cookie: SESSc0a59ad24620e681566ae51679e1e824=yes;"
    rxresp
    expect resp.status == 200
    txreq -url "/subsite"
    rxresp
    expect resp.status == 200

}

client c1 -run
